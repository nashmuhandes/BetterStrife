class BetterStrifeQuest : Thinker
{
	// Values correspond to the respective log numbers defined in the Strife IWAD.
	// This is the best way I can think of that doesn't force me to invest so much
	// time into defining the quest statuses by hand.
	enum ECurrentQuest
	{
		CQ_FINDHELP = 0,
		CQ_GETBELDINSRING = 1003,
		CQ_FOUNDBELDINSRING = CQ_GETBELDINSRING,
		CQ_LOOKFORREBELBASE1 = 2,
		CQ_LOOKFORREBELBASE2 = 3,
		CQ_LOOKFORREBELBASE3 = 4,
		CQ_TALKTOMACIL1 = 4,
		CQ_VISITIRALE1 = 1006,
		CQ_TALKTOGOVERNOR1 = 1006,
		CQ_TALKTOMACGUFFIN = 11,
		CQ_FOUNDBROKENCOUPLING = 15,
		CQ_DESTROYEDFRONTCOUPLING = 13,
		CQ_TOOKOUTSCANNINGTEAM = 14,
		CQ_GOTOPRISON = 1009,
		CQ_TALKTOWARDEN = 1009,
		CQ_DONETALKINGTOWARDEN = 1010,
		CQ_BROKEINTOPRISON = 18,
		CQ_LOOKFORJUDGE = 19,
		CQ_CUTJUDGESHAND = 20,
		CQ_FREEDPRISONERS = 21,
		CQ_TALKTOMACIL2 = CQ_FREEDPRISONERS,
	}

	bool initDone;
	int currentQuest;

	//===========================================================================
	//
	//
	//
	//===========================================================================

	BetterStrifeQuest Init(void)
	{
		ChangeStatNum(STAT_STATIC);

		if (!initDone)
		{
			// set the first quest when starting the game
			if (Level.totaltime == 0)
			{
				currentQuest = CQ_FINDHELP;
			}

			initDone = true;
		}

		return self;
	}

	//===========================================================================
	//
	//
	//
	//===========================================================================

	static clearscope BetterStrifeQuest GetInstance(void)
	{
		ThinkerIterator it = ThinkerIterator.Create("BetterStrifeQuest", STAT_STATIC);
		let p = BetterStrifeQuest(it.Next());
		if (p) return p;
		else return NULL;
	}

	//===========================================================================
	//
	//
	//
	//===========================================================================

	static clearscope int GetQuestLogNumber(void)
	{
		// FIX ME: this currently only checks quest status on the first player lol
		PlayerInfo plr = players[0];
		if (playeringame[0] && plr)
		{
			String txt = plr.LogText;
			int num = txt.Mid("\$TXT_ILOG".Length()).ToInt();
			return num;
		}
		return -1;
	}

	void SetQuest(int q)
	{
		currentQuest = q;
	}

	clearscope int GetCurrentQuest(void)
	{
		return currentQuest;
	}

	void UpdateQuest(void)
	{
		SetQuest(GetQuestLogNumber());
	}

	//===========================================================================
	//
	//
	//
	//===========================================================================

	override void Tick(void)
	{
		UpdateQuest();
		/*
		ThinkerIterator it = ThinkerIterator.Create("BetterStrifeQuestMarker");
		Actor mo;
		int count;
		while (mo = Actor(it.Next()))
		{
			count++;
		}
		Console.Printf("%d quest markers in this map", count);
		*/
	}
}
