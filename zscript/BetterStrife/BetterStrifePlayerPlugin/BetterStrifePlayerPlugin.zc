// Copyright 2021 Nash Muhandes
// All rights reserved.
//
// Redistribution and use in source and binary forms, with or without
// modification, are permitted provided that the following conditions
// are met:
//
// 1. Redistributions of source code must retain the above copyright
//    notice, this list of conditions and the following disclaimer.
// 2. Redistributions in binary form must reproduce the above copyright
//    notice, this list of conditions and the following disclaimer in the
//    documentation and/or other materials provided with the distribution.
// 3. The name of the author may not be used to endorse or promote products
//    derived from this software without specific prior written permission.
//
// THIS SOFTWARE IS PROVIDED BY THE AUTHOR ``AS IS'' AND ANY EXPRESS OR
// IMPLIED WARRANTIES, INCLUDING, BUT NOT LIMITED TO, THE IMPLIED WARRANTIES
// OF MERCHANTABILITY AND FITNESS FOR A PARTICULAR PURPOSE ARE DISCLAIMED.
// IN NO EVENT SHALL THE AUTHOR BE LIABLE FOR ANY DIRECT, INDIRECT,
// INCIDENTAL, SPECIAL, EXEMPLARY, OR CONSEQUENTIAL DAMAGES (INCLUDING, BUT
// NOT LIMITED TO, PROCUREMENT OF SUBSTITUTE GOODS OR SERVICES; LOSS OF USE,
// DATA, OR PROFITS; OR BUSINESS INTERRUPTION) HOWEVER CAUSED AND ON ANY
// THEORY OF LIABILITY, WHETHER IN CONTRACT, STRICT LIABILITY, OR TORT
// (INCLUDING NEGLIGENCE OR OTHERWISE) ARISING IN ANY WAY OUT OF THE USE OF
// THIS SOFTWARE, EVEN IF ADVISED OF THE POSSIBILITY OF SUCH DAMAGE.

//===========================================================================
//
// BetterStrifePlayerPlugin
//
//===========================================================================

class BetterStrifePlayerPlugin : Inventory
{
	transient CVar cvFlashlightEnabled;
	bool isFlashlightOn;

	//===========================================================================
	//
	//
	//
	//===========================================================================

	Default
	{
		Inventory.MaxAmount 1;
		+Inventory.UNDROPPABLE
		+Inventory.UNTOSSABLE
		+Inventory.AUTOACTIVATE
		-Inventory.INVBAR
	}

	//===========================================================================
	//
	//
	//
	//===========================================================================

	override bool TryPickup(in out Actor toucher)
	{
		if (!(toucher is "PlayerPawn") || (toucher is "PlayerPawn" && !toucher.player))
		{
			GoAwayAndDie();
			return false;
		}
		return Super.TryPickup(toucher);
	}

	override void Tick(void)
	{
		UpdateFlashlight();
		Super.Tick();
	}

	//===========================================================================
	//
	//
	//
	//===========================================================================

	clearscope static BetterStrifePlayerPlugin Find(Actor a)
	{
		BetterStrifePlayerPlugin plugin = BetterStrifePlayerPlugin(a.FindInventory("BetterStrifePlayerPlugin", true));
		return plugin;
	}

	//===========================================================================
	//
	//
	//
	//===========================================================================

	void SpawnFlashlight(void)
	{
		Owner.A_AttachLight
		(
			'BetterStrifePlayerFlashlightLight',
			DynamicLight.PointLight,
			Color(173, 204, 211),
			256.,
			0.,
			flags: DynamicLight.LF_ATTENUATE | DynamicLight.LF_DONTLIGHTSELF | DynamicLight.LF_SPOT,
			ofs:
			(
				0.0,
				7.85,
				PlayerPawn(Owner).ViewHeight
			),
			spoti: 15.0,
			spoto: 25.0,
			spotp: Owner.Pitch
		);
	}

	void DestroyFlashlight(void)
	{
		Owner.A_RemoveLight('BetterStrifePlayerFlashlightLight');
	}

	void UpdateFlashlight(void)
	{
		if (!Owner) return;
		if (Owner && !Owner.player) return;

		if (!cvFlashlightEnabled)
		{
			cvFlashlightEnabled = CVar.GetCVar("betterstrife_flashlight", Owner.player);
		}
		if (!cvFlashlightEnabled) return;

		// flashlight setting is enabled
		if (cvFlashlightEnabled.GetBool())
		{
			// give the flashlight toggler
			if (!Owner.FindInventory("BetterStrifePlayerFlashlight", true))
			{
				Owner.A_GiveInventory("BetterStrifePlayerFlashlight", 1);
			}

			// spawn or despawn the actual light
			if (isFlashlightOn)
			{
				SpawnFlashlight();
			}
			else
			{
				DestroyFlashlight();
			}
		}
		// flashlight setting is disabled
		else
		{
			if (Owner.FindInventory("BetterStrifePlayerFlashlight", true))
			{
				Owner.A_TakeInventory("BetterStrifePlayerFlashlight", 0x7FFFFFFF);
			}
			isFlashlightOn = false;
			DestroyFlashlight();
		}
	}

	//===========================================================================
	//
	//
	//
	//===========================================================================

	void UseHealthItems(void)
	{
		Array<Inventory> Items;

		for (Inventory inv = Owner.Inv; inv != NULL; inv = inv.Inv)
		{
			let hp = HealthPickup(inv);
			if (hp && hp.Amount > 0)
			{
				if (hp.autousemode == 3) Items.Push(inv);
			}
		}

		while (Items.Size() > 0)
		{
			int maxhealth = 0;
			int index = -1;

			// Find the largest item in the list
			for (int i = 0; i < Items.Size(); i++)
			{
				if (Items[i].health > maxhealth)
				{
					index = i;
					maxhealth = Items[i].Amount;
				}
			}

			while (Owner.player.health < PlayerPawn(Owner).GetMaxHealth(true))
			{
				let item = Items[index];
				if (item == NULL || !Owner.UseInventory(item))
					break;
			}
			if (Owner.player.health >= PlayerPawn(Owner).GetMaxHealth(true)) return;
			// Using all of this item was not enough so delete it and restart with the next best one
			Items.Delete(index);
		}
	}
}
